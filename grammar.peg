package stock_parser

type StockCodeParser Peg {
  pos int
  peekPos int
}

Item <- Line* !.
Line <- Stock / FTStock / XLStock / OTHER 

# Any other characters, to ignore
OTHER <- (.)

# Match stockcode
Stock <- ( 
  # $BABA$, $BABA.US$
  '$' Code (Suffix / Suffix?) '$' /

  # $BABA.US $700.HK 
  '$' Code Suffix /

  # BABA.US 700.HK
  ' '+ Code Suffix /

  # $BABA
  '$' USCode /

  # (NYSE: A)
  '(' ('NYSE' / 'NASDAQ') ('：' / ':') SP* USCode ')' /

  # (HK:2015) (US:BABA)
  '(' Market ':' Code ')' 
)

XLStock <- (
  # $阿里巴巴(BABA)$ $中国平安(SH601318)$ $腾讯控股(00700)$ $百度集团-SW(09988)$ $恒生科技指数(HKHSTECH)$ $恒生指数(HKHSI)$ 
  '$' StockName '(' (CNMarket ACode /  HKMarket HSCODE / USCode / HKCode) ')' '$'
)

FTStock <- (
  # $阿里巴巴(BABA.US)$ $腾讯控股(00700.HK)$  $歌尔股份(002241.SZ)$
  '$' (StockName / StockName '-' Letter+)  '(' Code '.' Market ')' '$'  
)

StockName <- [^()'-']+

Code <- (USCode / HKCode / ACode)

USCode <- Letter+
HKCode <- Number+
ACode <- Number+

HSCODE <- ('HSTECH' / 'HSI')


Letter <- [A-Z]
Number <- [0-9]

Suffix <- '.' (Market / "O")

Market <- (CNMarket / HKMarket / USMarket / SGMarket)
CNMarket <- ('SH' / 'SZ')
HKMarket <- 'HK'
USMarket <- 'US'
SGMarket <- 'SG'

# Any spaces
SP <- " " / "\t"



