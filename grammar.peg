package stock_parser

type StockCodeParser Peg {
  pos int
  peekPos int
}

Item <- Line* !.
Line <- FTStock / XLStock / Stock / OTHER 

# Any other characters, to ignore
OTHER <- (.)

# Match stockcode
Stock <- ( 
  # $BABA$, $BABA.US$
  '$' Code (Suffix / Suffix?) '$' /

  # $BABA.US $700.HK BABA.US 700.HK
  '$'? Code Suffix /

  # $BABA
  '$' USCode /

  # (NYSE: A)
  '(' ('NYSE' / 'NASDAQ') ('：' / ':') SP* USCode ')' 
)

XLStock <- (
  # $阿里巴巴(BABA)$ $中国平安(SH601318)$ $腾讯控股(00700)$
  '$' StockName '(' (CNMarket ACode / USCode / HKCode) ')' '$'
)

FTStock <- (
  # $阿里巴巴(BABA.US)$ $腾讯控股(00700.HK)$  $歌尔股份(002241.SZ)$
  '$' StockName '(' Code '.' Market ')' '$'  
)

StockName <- [^()]+

Code <- (USCode / HKCode / ACode)

USCode <- Letter+
HKCode <- Number+
ACode <- Number+


Letter <- [A-Z]
Number <- [0-9]

Suffix <- '.' (Market / "O")

Market <- (CNMarket / HKMarket / USMarket / SGMarket)
CNMarket <- ('SH' / 'SZ')
HKMarket <- 'HK'
USMarket <- 'US'
SGMarket <- 'SG'

# Any spaces
SP <- " " / "\t"



